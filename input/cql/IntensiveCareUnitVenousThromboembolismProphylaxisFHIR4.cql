/*
@update: 2022-06-23: based on AU 2022 content
rename this file to v'0.0.13'
added include FHIRCommon version '1.1.000' called FHIRCommon
removed valueset "Device Application": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1110.48 
removed valueset "Hemorrhagic Stroke": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.212'
removed valueset "Ischemic Stroke": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.247' 
added value set Application of Graduated Compression Stockings (GCS) 
added value set Application of Intermittent Pneumatic Compression Devices 
added value set Application of Venous Foot Pumps (VFP)
added value set Comfort Measures
Updated Low risk Indicate for VTE and 2 assocatie definitions by using Earliest()
Updated requestStatusReason by using qicore-doNOTperformReason for ServiceRequest and removed Function "GetStatusReason"
*/
library IntensiveCareUnitVenousThromboembolismProphylaxisFHIR4 version '0.0.013'

using FHIR version '4.0.1'

include FHIRHelpers version '4.1.000' called FHIRHelpers
include SupplementalDataElementsFHIR4 version '3.0.000' called SDE
include FHIRCommon version '1.1.000' called FHIRCommon
include VTEFHIR4 version '6.0.000' called VTE
include MATGlobalCommonFunctionsFHIR4 version '7.0.000' called Global

codesystem "LOINC": 'http://loinc.org' 

/*commented out the following vs due to unavalibilities in 2022 version of valuesets*/
//valueset "Application of Graduated Compression Stockings (GCS)":'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1110.66'
//valueset "Application of Intermittent Pneumatic Compression Devices (IPC)": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1110.65'
//valueset "Application of Venous Foot Pumps (VFP)": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1110.64'
valueset "Atrial Fibrillation/Flutter": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.202' 
valueset "Comfort Measures": 'http://cts.nlm.nih.gov/fhir/ValueSet/1.3.6.1.4.1.33895.1.3.0.45'
valueset "Direct Thrombin Inhibitor": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.205' 
valueset "Emergency Department Visit": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.292' 
valueset "General or Neuraxial Anesthesia": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.666.5.1743' 
valueset "General Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.255' 
valueset "Glycoprotein IIb/IIIa Inhibitors": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1045.41' 
valueset "Graduated compression stockings (GCS)": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.256' 
valueset "Gynecological Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.257' 
valueset "Hip Fracture Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.258' 
valueset "Hip Replacement Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.259' 
valueset "Injectable Factor Xa Inhibitor for VTE Prophylaxis": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.211' 
valueset "INR": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.213' 
valueset "Intensive Care Unit": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1029.206' 
valueset "Intermittent pneumatic compression devices (IPC)": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.214' 
valueset "Intracranial Neurosurgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.260' 
valueset "Intravenous route": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.222' 
valueset "Knee Replacement Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.261' 
valueset "Low Dose Unfractionated Heparin for VTE Prophylaxis": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1045.39' 
valueset "Low Molecular Weight Heparin for VTE Prophylaxis": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.219' 
valueset "Low Risk": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.400' 
valueset "Medical Reason": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.473' 
valueset "Mental Health Diagnoses": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.464.1003.105.12.1004' 
valueset "Obstetrics": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.263' 
valueset "Obstetrics VTE": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.264' 
valueset "Oral Factor Xa Inhibitor for VTE Prophylaxis or VTE Treatment": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.134' 
valueset "Patient Refusal": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.93' 
valueset "Rivaroxaban for VTE Prophylaxis": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113762.1.4.1110.50' 
valueset "Subcutaneous route": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.223' 
valueset "Unfractionated Heparin": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.218' 
valueset "Urological Surgery": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.272' 
valueset "Venous foot pumps (VFP)": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.230' 
valueset "Venous Thromboembolism": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.279' 
valueset "Warfarin": 'http://cts.nlm.nih.gov/fhir/ValueSet/2.16.840.1.113883.3.117.1.7.1.232' 

code "Risk for venous thromboembolism": '72136-5' from "LOINC" display 'Risk for venous thromboembolism'

parameter "Measurement Period" Interval<DateTime>

context Patient

define "SDE Ethnicity":
  SDE."SDE Ethnicity"

define "SDE Payer":
  SDE."SDE Payer"

define "SDE Race":
  SDE."SDE Race"

define "SDE Sex":
  SDE."SDE Sex"


define "Initial Population":
  "Encounter with Age Range and without VTE Diagnosis or Obstetrical Conditions"

define "Encounter with Age Range and without VTE Diagnosis or Obstetrical Conditions":
  ( Global."Inpatient Encounter" InpatientEncounter
              where "AgeInYearsAt"(start of InpatientEncounter.period) >= 18
              )
              intersect "Admission without VTE or Obstetrical Conditions"

define "Admission without VTE or Obstetrical Conditions":
  Global."Inpatient Encounter" InpatientEncounter
          		    where not (exists (Global.EncounterDiagnosis(InpatientEncounter)) EncounterDiagnosis
                        where (EncounterDiagnosis.code in "Obstetrics"
          								or EncounterDiagnosis.code in "Venous Thromboembolism"
          								or EncounterDiagnosis.code in "Obstetrics VTE"
          			         )
                  )

define "Denominator":
  "Encounter with ICU Location"

define "Encounter with ICU Location":
  "Encounter with Age Range and without VTE Diagnosis or Obstetrical Conditions" QualifyingEncounter
          	where exists (QualifyingEncounter.location Location
        				where Global.GetLocation(Location.location).type in "Intensive Care Unit"
          			and Location.period during QualifyingEncounter.period
          	)

define "Denominator Exclusions":
  "Encounter with ICU Location And Encounter Less Than 2 Days"
          union "Encounter with First ICU Stay with Principal Procedure of SCIP VTE Selected Surgery"
        	union "Encounter with Intervention Comfort Measures From Day Of Start of Hospitalization To Day After First ICU Stay"
          union "Encounter with Intervention Comfort Measures on Day of or Day After Procedure"

define "Encounter with ICU Location And Encounter Less Than 2 Days":
  "Encounter with ICU Location"QualifyingEncounterICU
          	where Global."LengthInDays"(QualifyingEncounterICU.period)< 2


define "SCIP VTE Selected Surgery":
  (	["Procedure": "General Surgery"]
          		union ["Procedure": "Gynecological Surgery"]
          		union ["Procedure": "Hip Fracture Surgery"]
          		union ["Procedure": "Hip Replacement Surgery"]
          		union ["Procedure": "Intracranial Neurosurgery"]
          		union ["Procedure": "Knee Replacement Surgery"]
          		union ["Procedure": "Urological Surgery"] ) Procedure
          		where Procedure.status = 'completed'


define "Encounter with First ICU Stay with Principal Procedure of SCIP VTE Selected Surgery":
  from
      "Encounter with ICU Location" QualifyingEncounterICU,
      "SCIP VTE Selected Surgery" SelectedProcedure
      let EncounterProcedure: FHIRCommon.Extension ( QualifyingEncounterICU, 'qicore-encounter-procedure' )
      where FHIRHelpers.ToInteger ( FHIRCommon.Extension  ( EncounterProcedure, 'rank' ).value as FHIR.positiveInt ) = 1
        and Global.GetId ( FHIRHelpers.ToString ( ( FHIRCommon.Extension  ( EncounterProcedure, 'procedure' ).value as FHIR.Reference ).reference ) ) = SelectedProcedure.id
        and date from ( 
          end of Global."Normalize Interval" ( SelectedProcedure.performed )
        ) during VTE."CalendarDayOfOrDayAfter" ( VTE."StartOfFirstICU" ( QualifyingEncounterICU ) )
      return QualifyingEncounterICU


define "Encounter with Intervention Comfort Measures From Day Of Start of Hospitalization To Day After First ICU Stay":
  "Encounter with ICU Location" QualifyingEncounterICU
              with "Intervention Comfort Measures" ComfortMeasure
            such that date from(Coalesce(start of Global."Normalize Interval"(ComfortMeasure.performed), ComfortMeasure.authoredOn)) during VTE."FromDayOfStartOfHospitalizationToDayAfterFirstICU"(QualifyingEncounterICU)


define "Intervention Comfort Measures":
  ( ["ServiceRequest": "Comfort Measures"] P
      where P.intent = 'order'
      and P.status in { 'active', 'on-hold', 'completed'}
  )
    union ( ["Procedure": "Comfort Measures"] InterventionPerformed
        where InterventionPerformed.status in { 'completed', 'in-progress' }
    )


define "Encounter with Intervention Comfort Measures on Day of or Day After Procedure":
  from
            "Encounter with ICU Location" QualifyingEncounterICU,
            ["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
            "Intervention Comfort Measures" ComfortMeasure
            where  AnesthesiaProcedure.status = 'completed'
              	and Global."Normalize Interval"(AnesthesiaProcedure.performed ) ends 1 day after day of VTE."StartOfFirstICU"(QualifyingEncounterICU)
                and date from (Coalesce(start of Global."Normalize Interval"(ComfortMeasure.performed), ComfortMeasure.authoredOn)) during VTE."CalendarDayOfOrDayAfter"(
            			end of Global."Normalize Interval"(AnesthesiaProcedure.performed))
            	return QualifyingEncounterICU


define "Numerator":
  "Encounter with VTE Prophylaxis Received on Day of or Day After First ICU Stay or Procedure"
        	union ( "Encounter with Medication Oral Factor Xa Inhibitor Administered on Day of or Day After First ICU Stay or Procedure"
        			intersect ( "Encounter with Prior or Present Diagnosis of Atrial Fibrillation or Prior Diagnosis of VTE"
        					union "Encounter with Prior or Present Procedure of Hip or Knee Replacement Surgery"
        			)
        	)
        	union "Encounter with Low Risk for VTE or Anticoagulant Administered"
        	union "Encounter with No VTE Prophylaxis Due to Medical Reason"
        	union "Encounter with No VTE Prophylaxis Due to Patient Refusal"

define "Pharmacological or Mechanical VTE Prophylaxis Received":
  ( ["MedicationAdministration":"Low Dose Unfractionated Heparin for VTE Prophylaxis"] VTEMedication
              where VTEMedication.status ='completed'
                   and VTEMedication.dosage.route in "Subcutaneous route"
          )
           union (["MedicationAdministration": "Low Molecular Weight Heparin for VTE Prophylaxis"] LMWH where LMWH.status = 'completed')
            union (["MedicationAdministration":  "Injectable Factor Xa Inhibitor for VTE Prophylaxis"] FactorXa where FactorXa.status = 'completed')
             union (["MedicationAdministration":  "Warfarin"] Warfarin where Warfarin.status = 'completed')
             union (["MedicationAdministration": "Rivaroxaban for VTE Prophylaxis"] Rivaroxaban where Rivaroxaban.status = 'completed')
              /*union (
                  (["Procedure": "Application of Intermittent Pneumatic Compression Devices (IPC)"] 
                  union ["Procedure": "Application of Venous Foot Pumps (VFP)"] 
                  union ["Procedure": "Application of Graduated Compression Stockings (GCS)"] ) DeviceApplied
                  where DeviceApplied.status = 'completed'
                            ) */

define "Encounter with VTE Prophylaxis Received on Day of or Day After First ICU Stay or Procedure":
  ( "Encounter with ICU Location" QualifyingEncounterICU
      with	"Pharmacological or Mechanical VTE Prophylaxis Received" VTEProphylaxis
        such that date from(start of Global."Normalize Interval"(VTEProphylaxis.effective)) during VTE."CalendarDayOfOrDayAfter"(VTE."StartOfFirstICU"(QualifyingEncounterICU))
        //such that date from(start of Coalesce(Global."Normalize Interval"(VTEProphylaxis.effective), Global."Normalize Interval"(VTEProphylaxis.performed))) during VTE."CalendarDayOfOrDayAfter"(VTE."StartOfFirstICU"(QualifyingEncounterICU))
        	)
        	union ( from
        				"Encounter with ICU Location" QualifyingEncounterICU,
        				["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
        				"Pharmacological or Mechanical VTE Prophylaxis Received" VTEProphylaxis
        				where Global."Normalize Interval"(AnesthesiaProcedure.performed) ends 1 day after day of VTE."StartOfFirstICU"(QualifyingEncounterICU)
        					and date from(start of Global."Normalize Interval"(VTEProphylaxis.effective)) during VTE."CalendarDayOfOrDayAfter"(
        					//and date from(start of Coalesce(Global."Normalize Interval"(VTEProphylaxis.effective), Global."Normalize Interval"(VTEProphylaxis.performed))) during VTE."CalendarDayOfOrDayAfter"(
          				end of Global."Normalize Interval"(AnesthesiaProcedure.performed))
                    return QualifyingEncounterICU
                )
               

define "Encounter with Medication Oral Factor Xa Inhibitor Administered on Day of or Day After First ICU Stay or Procedure":
  (  "Encounter with ICU Location" QualifyingEncounterICU
     with ["MedicationAdministration":  "Oral Factor Xa Inhibitor for VTE Prophylaxis or VTE Treatment"] FactorXaMedication
     such that FactorXaMedication.status = 'completed'
                  and date from(start of Global."Normalize Interval"(FactorXaMedication.effective)) during VTE."CalendarDayOfOrDayAfter"(VTE."StartOfFirstICU"(QualifyingEncounterICU))
           )
          union ( from
            			 "Encounter with ICU Location" QualifyingEncounterICU,
            			["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
            			["MedicationAdministration":  "Oral Factor Xa Inhibitor for VTE Prophylaxis or VTE Treatment"] FactorXaMedication
            			where FactorXaMedication.status = 'completed'
                    and AnesthesiaProcedure.status = 'completed'
            				and Global."Normalize Interval"(AnesthesiaProcedure.performed) ends 1 day after day of VTE."StartOfFirstICU"(QualifyingEncounterICU)
            					and date from(start of Global."Normalize Interval"(FactorXaMedication.effective)) during VTE."CalendarDayOfOrDayAfter"(
              				end of Global."Normalize Interval"(AnesthesiaProcedure.performed))
            			return QualifyingEncounterICU
                )


define "Encounter with Prior or Present Diagnosis of Atrial Fibrillation or Prior Diagnosis of VTE":
  ( "Encounter with ICU Location" QualifyingEncounterICU 
      with ["Condition": "Atrial Fibrillation/Flutter"] AtrialFibrillation
        such that ( FHIRHelpers.ToConcept ( AtrialFibrillation.clinicalStatus ) ~ FHIRCommon."active"
            or FHIRHelpers.ToConcept ( AtrialFibrillation.clinicalStatus ) ~ FHIRCommon."recurrence"
            or FHIRHelpers.ToConcept ( AtrialFibrillation.clinicalStatus ) ~ FHIRCommon."relapse"
        )
          and FHIRHelpers.ToConcept ( AtrialFibrillation.verificationStatus ) ~ FHIRCommon."confirmed"
          and Global."Normalize Interval" ( AtrialFibrillation.onset ) starts on or before 
          end of QualifyingEncounterICU.period
  )
    union ( "Encounter with ICU Location" QualifyingEncounterICU
        where Global.EncounterDiagnosis ( QualifyingEncounterICU ).code in "Atrial Fibrillation/Flutter"
    )
    union ( "Encounter with ICU Location" QualifyingEncounterICU
        with ["Condition": "Venous Thromboembolism"] VTEDiagnosis
          such that ( FHIRHelpers.ToConcept ( VTEDiagnosis.clinicalStatus ) ~ FHIRCommon."inactive"
              or FHIRHelpers.ToConcept ( VTEDiagnosis.clinicalStatus ) ~ FHIRCommon."remission"
              or FHIRHelpers.ToConcept ( VTEDiagnosis.clinicalStatus ) ~ FHIRCommon."resolved"
          )
            and VTEDiagnosis.verificationStatus ~ FHIRCommon."confirmed"
            and Global."Normalize Interval" ( VTEDiagnosis.onset ) before start of QualifyingEncounterICU.period
    )


define "Encounter with Prior or Present Procedure of Hip or Knee Replacement Surgery":
  "Encounter with ICU Location" QualifyingEncounterICU
        	with ( ["Procedure": "Hip Replacement Surgery"]
          			union ["Procedure": "Knee Replacement Surgery"] ) HipKneeProcedure
        	such that HipKneeProcedure.status = 'completed'
        			and Global."Normalize Interval"(HipKneeProcedure.performed)  starts on or before end of QualifyingEncounterICU.period


define "Encounter with Low Risk for VTE or Anticoagulant Administered":
  "Low Risk for VTE or Anticoagulant Administered From Day of Start of Hospitalization To Day After First ICU Stay"
          union "Low Risk for VTE or Anticoagulant Administered on Day of or Day After Procedure"


//Need find out 1. if category check is needed 2. which one should be used for 'risk for VTE'
define "Low Risk Indicator For VTE":
  ( ["Observation": "Risk for venous thromboembolism"] VTERiskAssessment
      where /* exists (VTERiskAssessment.category C
                where FHIRHelpers.ToConcept(C) ~ FHIRCommon."exam" )
        and */
        FHIRHelpers.ToConcept ( VTERiskAssessment.value ) in "Low Risk"
        and VTERiskAssessment.status in { 'final', 'amended', 'corrected' }
      return {
        id: VTERiskAssessment.id,
        LowRiskDatetime: Global.Earliest(VTERiskAssessment.effective)
      }
  )
    union ( ["Observation": "INR"] INRLabTest
        where /* exists (INRLabTest.category C
                where FHIRHelpers.ToConcept(C) ~ FHIRCommon."laboratory" )
        and */
        INRLabTest.value as Quantity > 3.0
          and INRLabTest.status in { 'final', 'amended', 'corrected' }
        return  { 
          id: INRLabTest.id, 
          LowRiskDatetime: INRLabTest.issued }
    )
    union ( ( ( ["MedicationAdministration": "Unfractionated Heparin"] UnfractionatedHeparin
          where UnfractionatedHeparin.dosage.route in "Intravenous route"
      )
        union ["MedicationAdministration": "Direct Thrombin Inhibitor"]
        union ["MedicationAdministration": "Glycoprotein IIb/IIIa Inhibitors"] ) AnticoagulantMedication
        where AnticoagulantMedication.status = 'completed'
        return { 
          id: AnticoagulantMedication.id, 
          LowRiskDatetime: start of Global."Normalize Interval"(AnticoagulantMedication.effective) }
    )


define "Low Risk for VTE or Anticoagulant Administered From Day of Start of Hospitalization To Day After First ICU Stay":
  "Encounter with ICU Location" QualifyingEncounterICU
          with "Low Risk Indicator For VTE" LowRiskForVTE
          such that LowRiskForVTE.LowRiskDatetime during day of VTE."FromDayOfStartOfHospitalizationToDayAfterFirstICU"(QualifyingEncounterICU)

define "Low Risk for VTE or Anticoagulant Administered on Day of or Day After Procedure":
  from
          	"Encounter with ICU Location" QualifyingEncounterICU,
          	["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
          	"Low Risk Indicator For VTE" LowRiskForVTE
          where AnesthesiaProcedure.status = 'completed'
            and Global."Normalize Interval"(AnesthesiaProcedure.performed) ends 1 day after day of VTE."StartOfFirstICU"(QualifyingEncounterICU)
          	and LowRiskForVTE.LowRiskDatetime during day of VTE."CalendarDayOfOrDayAfter"(end of Global."Normalize Interval"(AnesthesiaProcedure.performed))
          return QualifyingEncounterICU


define "Encounter with No VTE Prophylaxis Due to Medical Reason":
  ( "No VTE Prophylaxis Medication Due to Medical Reason From Day Of Start of Hospitalization To Day After First ICU Stay"
            intersect "No Mechanical VTE Prophylaxis Due to Medical Reason From Day of Start of Hospitalization To Day After First ICU Stay"
          )
          union ( "No VTE Prophylaxis Medication Due to Medical Reason on Day of or Day After Procedure"
              intersect "No Mechanical VTE Prophylaxis Due to Medical Reason on Day of or Day After Procedure"
          )


define "No VTE Prophylaxis Medication Due to Medical Reason From Day Of Start of Hospitalization To Day After First ICU Stay":
  "Encounter with ICU Location" QualifyingEncounterICU
      with "No VTE Prophylaxis Medication Administered or Ordered" NoVTEMedication
        such that NoVTEMedication.MedicationStatusReason in "Medical Reason"
          and date from ( NoVTEMedication.authoredOn ) during VTE."FromDayOfStartOfHospitalizationToDayAfterFirstICU" ( QualifyingEncounterICU )         


define "No VTE Prophylaxis Medication Administered or Ordered":
  ( ( ["MedicationAdministration": "Low Dose Unfractionated Heparin for VTE Prophylaxis"]
        union ["MedicationAdministration": "Low Molecular Weight Heparin for VTE Prophylaxis"]
        union ["MedicationAdministration": "Injectable Factor Xa Inhibitor for VTE Prophylaxis"]
        union ["MedicationAdministration": "Warfarin"]
        union ["MedicationAdministration": "Rivaroxaban for VTE Prophylaxis"] ) MedicationAdm
        let MedicationNotDoneTiming: FHIRCommon.Extension ( MedicationAdm, 'qicore-recorded' ).value
        where MedicationAdm.status = 'not-done'
        return {
          id: MedicationAdm.id,
          MedicationStatusReason: MedicationAdm.statusReason,
          authoredOn: MedicationNotDoneTiming
        }
    )
      union ( ( ["MedicationRequest": "Low Dose Unfractionated Heparin for VTE Prophylaxis"]
          union ["MedicationRequest": "Low Molecular Weight Heparin for VTE Prophylaxis"]
          union ["MedicationRequest": "Injectable Factor Xa Inhibitor for VTE Prophylaxis"]
          union ["MedicationRequest": "Warfarin"]
          union ["MedicationRequest": "Rivaroxaban for VTE Prophylaxis"]) MedicationOrder
          where MedicationOrder.doNotPerform is true
            and MedicationOrder.status in { 'completed', 'cancelled' }
          return {
            id: MedicationOrder.id,
            MedicationStatusReason: MedicationOrder.reasonCode,
            authoredOn: MedicationOrder.authoredOn
          }
      )



define "No Mechanical VTE Prophylaxis Due to Medical Reason From Day of Start of Hospitalization To Day After First ICU Stay":
  "Encounter with ICU Location" QualifyingEncounterICU
            with "No Mechanical VTE Prophylaxis Performed or Ordered" NoVTEDevice
            such that NoVTEDevice.requestStatusReason in "Medical Reason"
                and date from(NoVTEDevice.authoredOn) during VTE."FromDayOfStartOfHospitalizationToDayAfterFirstICU"(QualifyingEncounterICU)


define "No Mechanical VTE Prophylaxis Performed or Ordered":
  ( ( ["ServiceRequest": "Venous foot pumps (VFP)"]
      union ["ServiceRequest": "Intermittent pneumatic compression devices (IPC)"]
      union ["ServiceRequest": "Graduated compression stockings (GCS)"] ) DeviceOrder
      where DeviceOrder.status = 'completed'
        and DeviceOrder.doNotPerform is true
      return {
        id: DeviceOrder.id,
        requestStatusReason: FHIRHelpers.ToConcept(FHIRCommon.Extension (DeviceOrder, 'qicore-doNotPerformReason').value),
        authoredOn: DeviceOrder.authoredOn
      }
  )
   /* union ( (["Procedure": "Application of Intermittent Pneumatic Compression Devices (IPC)"] 
                  union ["Procedure": "Application of Venous Foot Pumps (VFP)"] 
                  union ["Procedure": "Application of Graduated Compression Stockings (GCS)"]) DeviceApplied
        let DeviceNotDoneTiming: FHIRCommon.Extension ( DeviceApplied, 'qicore-recorded' ).value
        where DeviceApplied.status = 'not-done'
        return {
          id: DeviceApplied.id,
          requestStatusReason: DeviceApplied.statusReason,
          authoredOn: DeviceNotDoneTiming
        }
    )  */


define "No VTE Prophylaxis Medication Due to Medical Reason on Day of or Day After Procedure":
  from
      "Encounter with ICU Location" QualifyingEncounterICU,
      ["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
      "No VTE Prophylaxis Medication Administered or Ordered" NoVTEMedication
      where NoVTEMedication.MedicationStatusReason in "Medical Reason"
        and AnesthesiaProcedure.status = 'completed'
        and Global."Normalize Interval" ( AnesthesiaProcedure.performed ) ends 1 day after day of VTE."StartOfFirstICU" ( QualifyingEncounterICU )
        and date from ( NoVTEMedication.authoredOn ) during VTE."CalendarDayOfOrDayAfter" ( 
        end of Global."Normalize Interval" ( AnesthesiaProcedure.performed as Period ) )
      return QualifyingEncounterICU                        


define "No Mechanical VTE Prophylaxis Due to Medical Reason on Day of or Day After Procedure":
  from
        		"Encounter with ICU Location" QualifyingEncounterICU,
        		["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
        		"No Mechanical VTE Prophylaxis Performed or Ordered" NoVTEDevice
        		where NoVTEDevice.requestStatusReason in "Medical Reason"
                and AnesthesiaProcedure.status = 'completed'
                and Global."Normalize Interval"(AnesthesiaProcedure.performed ) ends 1 day after day of VTE."StartOfFirstICU"(QualifyingEncounterICU)
                and date from (NoVTEDevice.authoredOn) during VTE."CalendarDayOfOrDayAfter"(
        			         end of Global."Normalize Interval"(AnesthesiaProcedure.performed))
        		return QualifyingEncounterICU

        

define "Encounter with No VTE Prophylaxis Due to Patient Refusal":
  "No VTE Prophylaxis Due to Patient Refusal From Day of Start of Hospitalization To Day After First ICU Stay"
        	union "No VTE Prophylaxis Due to Patient Refusal on Day of or Day After Procedure"


define "No VTE Prophylaxis Due to Patient Refusal From Day of Start of Hospitalization To Day After First ICU Stay":
  "Encounter with ICU Location" QualifyingEncounterICU
      with "No Mechanical or Pharmacological VTE Prophylaxis Due to Patient Refusal" PatientRefusal
        such that date from ( PatientRefusal.authoredOn ) during VTE.FromDayOfStartOfHospitalizationToDayAfterFirstICU ( QualifyingEncounterICU )

define "No VTE Prophylaxis Due to Patient Refusal on Day of or Day After Procedure":
  from
      "Encounter with ICU Location" QualifyingEncounterICU,
      ["Procedure": "General or Neuraxial Anesthesia"] AnesthesiaProcedure,
      "No Mechanical or Pharmacological VTE Prophylaxis Due to Patient Refusal" PatientRefusal
      where Global."Normalize Interval" ( AnesthesiaProcedure.performed ) ends 1 day after day of VTE."StartOfFirstICU" ( QualifyingEncounterICU )
        and date from ( PatientRefusal.authoredOn ) during VTE."CalendarDayOfOrDayAfter" ( 
        end of Global."Normalize Interval" ( AnesthesiaProcedure.performed ) )
      return QualifyingEncounterICU

define "No Mechanical or Pharmacological VTE Prophylaxis Due to Patient Refusal":
  ( "No VTE Prophylaxis Medication Administered or Ordered" NoVTEMedication
        where NoVTEMedication.MedicationStatusReason in "Patient Refusal"
    )
      union ( "No Mechanical VTE Prophylaxis Performed or Ordered" NoVTEDevice
          where NoVTEDevice.requestStatusReason in "Patient Refusal"
      )


define "Denominator Exceptions":
  "Encounter with First ICU Location Stay Less Than 1 day"

define "Encounter with First ICU Location Stay Less Than 1 day":
  "Encounter with ICU Location" QualifyingEncounterICU
        	 where Global."LengthInDays"(VTE."FirstICULocationPeriod"(QualifyingEncounterICU)) < 1


/* no longer useful due to qicore profile changes
define function "GetStatusReason"(requestReason ServiceRequest ):
  FHIRCommon.Extension ( requestReason, 'request-statusReason' )
*/